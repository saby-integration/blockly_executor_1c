
#Область ВыполнитьКоманды_Клиент

&НаКлиенте
Процедура ВыполнитьКоманды_Клиент(ОбработчикРезультата, commands, commands_data) Экспорт
	
	commands_result = Новый Массив;
	commands_data = ПолучитьИзВременногоХранилища(commands_data);
	
	Контекст = Новый Структура;
	Контекст.Вставить("commands", commands);
	Контекст.Вставить("commands_data", commands_data);
	Контекст.Вставить("commands_result", commands_result);
	
	Контекст.Вставить("ОбработчикРезультата", ОбработчикРезультата);
	
	Контекст.Вставить("ТекИндекс", -1);
	Контекст.Вставить("Количество", commands.Количество());
	
	ВыполнитьКоманды_КлиентЦиклНачало(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКоманды_КлиентЦиклНачало(Контекст)
	
	Контекст.ТекИндекс = Контекст.ТекИндекс + 1;
	Если Контекст.ТекИндекс < Контекст.Количество Тогда
		ВыполнитьКоманды_КлиентОбработкаКоманды(Контекст);
	Иначе
		ВыполнитьКоманды_КлиентЗавершение(Контекст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКоманды_КлиентОбработкаКоманды(Контекст)
	
	commands_data = Контекст.commands_data;
	commands_result = Контекст.commands_result;
	
	command = Контекст.commands[Контекст.ТекИндекс];
	Если command.name <> "Подписать" Тогда
		command_result = Новый Структура("uuid, status, result", command.uuid, "error", "Неизвестная команда = '" + command.name + "'");
		commands_result.Добавить(command_result);
		ВыполнитьКоманды_КлиентЦиклНачало(Контекст);
	КонецЕсли;
	
	ОбработчикРезультата = Новый ОписаниеОповещения("ВыполнитьКоманды_КлиентПослеПодписания", ЭтотОбъект, Контекст);
	Подписать(ОбработчикРезультата, command.params.СертификатДляПодписания, commands_data.Получить(command.uuid));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКоманды_КлиентПослеПодписания(РезультатПодписания, Контекст) Экспорт
	
	command = Контекст.commands[Контекст.ТекИндекс];
	
	// Обработка ошибки
	Если ТипЗнч(РезультатПодписания) = Тип("Структура")
		И РезультатПодписания.Свойство("БылаОшибка") = Истина
		И РезультатПодписания.БылаОшибка = Истина Тогда
		
		command_result = Новый Структура("uuid, status, result", command.uuid, "error", РезультатПодписания.ИнформацияОбОшибке);
		
	// Нормальные данные
	Иначе
		command_result = Новый Структура("uuid, status, result", command.uuid, "complete", РезультатПодписания);
	КонецЕсли;
	Контекст.commands_result.Добавить(command_result);
	
	ВыполнитьКоманды_КлиентЦиклНачало(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКоманды_КлиентЗавершение(Контекст)
	
	ВыполнитьОбработкуОповещения(Контекст.ОбработчикРезультата, Контекст.commands_result);
	
КонецПроцедуры

#КонецОбласти

